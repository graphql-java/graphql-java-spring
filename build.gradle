import java.text.SimpleDateFormat

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}


def getDevelopmentVersion() {
    def output = new StringBuilder()
    def error = new StringBuilder()
    def gitShortHash = ["git", "-C", projectDir.toString(), "rev-parse", "--short", "HEAD"].execute()
    gitShortHash.waitForProcessOutput(output, error)
    def gitHash = output.toString().trim()
    if (gitHash.isEmpty()) {
        println "git hash is empty: error: ${error.toString()}"
        throw new IllegalStateException("git hash could not be determined")
    }
    def version = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date()) + "-" + gitHash
    println "created development version: $version"
    version
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    def msg = String.format("This build must be run with java 1.8 - you are running %s - gradle finds the JDK via JAVA_HOME=%s",
            JavaVersion.current(), System.getenv("JAVA_HOME"))
    throw new GradleException(msg)
}


repositories {
    mavenCentral()
    mavenLocal()
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.env.MAVEN_CENTRAL_USER
            password = System.env.MAVEN_CENTRAL_PASSWORD
        }
    }
}
group = 'com.graphql-java'

subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'groovy'
    apply plugin: 'signing'


    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    def releaseVersion = System.env.RELEASE_VERSION
    version = releaseVersion ? releaseVersion : getDevelopmentVersion()

    ext {
        graphqlJavaVersion = "15.0"
        springVersion = "5.1.7.RELEASE"
        springBootVersion = "2.1.5.RELEASE"
        jacksonVersion = "2.9.8"
        assertJVersion = "3.11.1"
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }


    task sourcesJar(type: Jar) {
        dependsOn classes
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {

        publications {

            graphqlJava(MavenPublication) {
                version version
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
                        name project.name
                        description project.description
                        url "https://github.com/graphql-java/graphql-java-spring"
                        scm {
                            url "https://github.com/graphql-java/graphql-java-spring"
                            connection "https://github.com/graphql-java/graphql-java-spring"
                            developerConnection "https://github.com/graphql-java/graphql-java-spring"
                        }
                        licenses {
                            license {
                                name 'MIT'
                                url 'https://github.com/graphql-java/graphql-java-spring/blob/master/LICENSE.md'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'andimarek'
                                name 'Andreas Marek'
                            }
                        }
                    }
                }
            }
        }
    }


// all publish tasks depend on the build task
    tasks.withType(PublishToMavenRepository) {
        dependsOn build
    }

    signing {
        def signingKey = System.env.MAVEN_CENTRAL_PGP_KEY
        useInMemoryPgpKeys(signingKey, "")
        sign publishing.publications
    }


}


task myWrapper(type: Wrapper) {
    gradleVersion = '6.6.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

